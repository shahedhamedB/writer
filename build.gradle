// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.6.0"
    repositories {
        google()
        jcenter()


    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28.3-alpha'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.5"
        classpath 'com.google.gms:google-services:4.3.8'
    }
    repositories {
        google()
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon

        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://zendesk.jfrog.io/artifactory/repo"
        }


    }
}


subprojects {
    if (project.name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    apply plugin: "kotlin-android"
    apply plugin: "kotlin-android-extensions"
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dagger.hilt.android.plugin'


    android {
        compileSdkVersion 31
        buildToolsVersion "30.0.3"

        configurations.all {
            resolutionStrategy { force 'androidx.core:core-ktx:1.7.0-alpha01' }
        }

        buildFeatures {
            // dataBinding = true
            viewBinding = true
        }

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 31

            versionCode 1
            versionName "1.0"
            vectorDrawables.useSupportLibrary = true
            multiDexEnabled true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }


        kotlinOptions {
            freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
            jvmTarget = '1.8'
        }

        configurations.all {

            resolutionStrategy {
                force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            }
        }
    }

    apply from: rootDir.path + "/dependencies.gradle"
}


task clean(type: Delete) {
    delete rootProject.buildDir
}